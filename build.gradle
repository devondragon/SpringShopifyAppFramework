plugins {
	id 'org.springframework.boot' version '4.0.1'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'java'
	id "com.github.ben-manes.versions" version "0.53.0"
	id "com.github.node-gradle.node" version "7.1.0"
	id 'org.owasp.dependencycheck' version '12.1.9'
}

group = 'com.justblackmagic'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
	dev
}

repositories {
	mavenCentral()
	// maven { url 'https://repo.spring.io/milestone' }
    // maven { url 'https://repo.spring.io/snapshot' }
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-jersey'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
	implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity6:3.1.3.RELEASE'
	implementation 'nz.net.ultraq.thymeleaf:thymeleaf-layout-dialect:3.4.0'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	// Joda-Time removed - using java.time instead
	implementation 'org.apache.commons:commons-lang3:3.20.0'
	implementation 'org.apache.commons:commons-text:1.15.0'
	implementation 'com.github.rholder:guava-retrying:2.0.0'
	// Jackson 3 is provided by Spring Boot 4.0 - no explicit dependencies needed
	implementation 'commons-io:commons-io:2.21.0'
	implementation 'com.bucket4j:bucket4j_jdk17-core:8.16.0'
	implementation 'com.github.ben-manes.caffeine:caffeine:3.1.8'
 	implementation 'io.netty:netty-resolver-dns-native-macos:4.1.98.Final:osx-aarch_64'

    implementation 'io.jsonwebtoken:jjwt-api:0.13.0'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.13.0'
	runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.13.0'

	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'org.mariadb.jdbc:mariadb-java-client:3.5.7'

	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	// runtimeOnly 'io.micrometer:micrometer-registry-new-relic'
	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testImplementation 'com.h2database:h2'
	implementation 'com.github.rest-driver:rest-driver:2.0.1'
}

tasks.named('test') {
  useJUnitPlatform()
}

node {
    version = '18'
}

task buildReactApp(type: NodeTask, dependsOn: 'npmInstall') {
    script = project.file('node_modules/webpack/bin/webpack.js')
    args = [
            '--mode', 'development',
            '--entry', './src/main/webapp/javascript/MyApp.jsx',
            '-o', './src/main/resources/static/dist'
    ]
}

processResources.dependsOn 'buildReactApp'


bootJar {
    // launchScript removed in Spring Boot 4.0 - embedded launch scripts no longer supported
    // If you need the confFolder property, configure it via external application properties instead
}

bootRun {
    // Use Spring Boot DevTool only when we run Gradle bootRun task
    classpath = sourceSets.main.runtimeClasspath + configurations.dev

    if (project.hasProperty('profiles')) {
		environment SPRING_PROFILES_ACTIVE: profiles
	} else {
		def profiles = 'local'
		environment SPRING_PROFILES_ACTIVE: profiles
	}
}

// OWASP Dependency Check configuration
// Run with: ./gradlew dependencyCheckAnalyze
dependencyCheck {
    // Fail the build if any vulnerability with CVSS score >= 7 is found
    failBuildOnCVSS = 7.0f

    // Suppress false positives using a suppressions file (create if needed)
    if (file('dependency-check-suppressions.xml').exists()) {
        suppressionFile = 'dependency-check-suppressions.xml'
    }

    // Output formats for reports
    formats = ['HTML', 'JSON']

    // Skip analyzing test dependencies for faster scans
    skipTestGroups = true

    // NVD API configuration (optional - for faster scans, get an API key from https://nvd.nist.gov/developers/request-an-api-key)
    // nvd {
    //     apiKey = System.getenv('NVD_API_KEY') ?: ''
    // }
}
